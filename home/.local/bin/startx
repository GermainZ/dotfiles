#!/usr/bin/zsh
# startx
# Alternative, simpler startx which makes use of the XDG specification and
# eventually systemd.
# Original: https://github.com/Earnestly/dotfiles/blob/master/.local/bin/startx

# Make sure we aren't running in an xserver
if [[ -n "$DISPLAY" ]]; then
    exit 1
fi

readonly confdir="${XDG_CONFIG_HOME:-$HOME/.config}"/X11
readonly cachedir="${XDG_CACHE_HOME:-$HOME/.cache}"/X11
readonly rundir="$XDG_RUNTIME_DIR"/X11

xinitrc="${XINITRC:-$confdir/xinitrc}"

# Make any necessary directories, bail if any fail.
if ! mkdir -p "$confdir" "$rundir"; then
    exit 1
fi

if [[ -n "$XDG_VTNR" ]]; then
    export DISPLAY=:"$XDG_VTNR"
else
    printf >&2 'Apparently XDG_VTNR is not set, bailing out.\n'
    exit 1
fi

serveropts=(
    vt"$XDG_VTNR" 
    -keeptty
)

exec xinit "${1:-$xinitrc}" -- "$DISPLAY" "${serveropts[@]}" "${@:2}"
